//file:noinspection UnnecessaryQualifiedReference
//file:noinspection GroovyAssignabilityCheck
//file:noinspection GradlePackageUpdate
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "dev.architectury.architectury-pack200" version "0.1.3"
    id "io.github.juuxel.loom-quiltflower-mini" version "171a6e2e49"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "gg.essential.loom" version "0.10.0.3"
    id "net.kyori.blossom" version "1.3.0"
    id "java"
}

java {
    withJavadocJar()
    withSourcesJar()
}

version = mod_version
group = "cc.polyfrost"
archivesBaseName = mod_name

blossom {
    String className = "src/main/java/cc/polyfrost/oneconfig/OneConfig.java"
    replaceToken("@VER@", project.version, className)
    replaceToken("@NAME@", mod_name, className)
    replaceToken("@ID@", mod_id, className)
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

loom {
    launchConfigs {
        client {
            property("fml.coreMods.load", "cc.polyfrost.oneconfig.plugin.LoadingPlugin")
            property("mixin.debug.export", "true")
        }
    }
    runConfigs {
        client {
            ideConfigGenerated = true
        }
    }
    forge {
        pack200Provider = new dev.architectury.pack200.java.Pack200Adapter()
        mixinConfig("mixins.oneconfig.json")
        mixin.defaultRefmapName.set("mixins.oneconfig.refmap.json")
    }
}

configurations {
    include
    implementation.extendsFrom(include)
    lwjgl
    lwjglNative {
        transitive = false
    }
    dummyImpl
    implementation.extendsFrom(dummyImpl)
}

sourceSets {
    dummy
    main {
        compileClasspath += dummy.output
        output.resourcesDir = java.classesDirectory
    }
}

sourceSets.dummy.compileClasspath += configurations.dummyImpl
sourceSets.main.runtimeClasspath += configurations.lwjglNative

repositories {
    maven { url 'https://repo.woverflow.cc/' }
}

task lwjglJar(type: ShadowJar) {
    group = "shadow"
    archiveClassifier.set("lwjgl")
    configurations = [project.configurations.lwjgl]
    exclude "META-INF/versions/**"
    exclude "**/module-info.class"
    exclude "**/package-info.class"
    relocate("org.lwjgl", "org.lwjgl3") {
        include "org.lwjgl.PointerBuffer"
        include "org.lwjgl.BufferUtils"
    }
}

dependencies {
    minecraft("com.mojang:minecraft:1.8.9")
    mappings("de.oceanlabs.mcp:mcp_stable:22-1.8.9")
    forge("net.minecraftforge:forge:1.8.9-11.15.1.2318-1.8.9")

    dummyImpl("gg.essential:vigilance-1.8.9-forge:222")

    include("gg.essential:universalcraft-1.8.9-forge:209") {
        transitive = false
    }

    // for other mods and universalcraft
    include("org.jetbrains.kotlin:kotlin-stdlib:1.6.21")
    include("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.21")
    include("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21")
    include("org.jetbrains.kotlin:kotlin-reflect:1.6.21")
    include("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1")
    include("org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.1")
    include("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.6.1")
    include("org.jetbrains.kotlinx:kotlinx-serialization-core-jvm:1.3.2")
    include("org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.3.2")
    include("org.jetbrains.kotlinx:kotlinx-serialization-cbor-jvm:1.3.2")

    include("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }

    lwjgl "org.lwjgl:lwjgl:3.3.1"
    lwjgl "org.lwjgl:lwjgl-stb:3.3.1"
    lwjgl "org.lwjgl:lwjgl-tinyfd:3.3.1"
    lwjgl "org.lwjgl:lwjgl-nanovg:3.3.1"
    lwjglNative "org.lwjgl:lwjgl:3.3.1:natives-windows"
    lwjglNative "org.lwjgl:lwjgl-stb:3.3.1:natives-windows"
    lwjglNative "org.lwjgl:lwjgl-tinyfd:3.3.1:natives-windows"
    lwjglNative "org.lwjgl:lwjgl-nanovg:3.3.1:natives-windows"
    lwjglNative "org.lwjgl:lwjgl:3.3.1:natives-linux"
    lwjglNative "org.lwjgl:lwjgl-stb:3.3.1:natives-linux"
    lwjglNative "org.lwjgl:lwjgl-tinyfd:3.3.1:natives-linux"
    lwjglNative "org.lwjgl:lwjgl-nanovg:3.3.1:natives-linux"
    lwjglNative "org.lwjgl:lwjgl:3.3.1:natives-macos"
    lwjglNative "org.lwjgl:lwjgl-stb:3.3.1:natives-macos"
    lwjglNative "org.lwjgl:lwjgl-tinyfd:3.3.1:natives-macos"
    lwjglNative "org.lwjgl:lwjgl-nanovg:3.3.1:natives-macos"
    include lwjglJar.outputs.files
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", version
    inputs.property "name", mod_name
    inputs.property "id", mod_id

    filesMatching("mcmod.info") {
        expand(
                "id": mod_id,
                "name": mod_name,
                "version": version
        )
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    archiveClassifier.set('dev')
    configurations = [project.configurations.include, project.configurations.lwjglNative]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude(
            "**/LICENSE.md",
            "**/LICENSE.txt",
            "**/LICENSE",
            "**/NOTICE",
            "**/NOTICE.txt",
            "pack.mcmeta",
            "dummyThing",
            "**/module-info.class",
            "META-INF/proguard/**",
            "META-INF/maven/**",
            "META-INF/versions/**",
            "META-INF/com.android.tools/**",
            "fabric.mod.json"
    )
}

remapJar {
    archiveClassifier.set('')
    from(shadowJar.archiveFile)
}

jar {
    manifest.attributes(
            'ModSide': 'CLIENT',
            'ForceLoadAsMod': true,
            "TweakOrder": "0",
            "MixinConfigs": "mixins.oneconfig.json",
            "FMLCorePlugin": "cc.polyfrost.oneconfig.plugin.LoadingPlugin",
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            "FMLCorePluginContainsFMLMod": "lol"
    )
    enabled = false
}
jar.dependsOn(shadowJar)

project.afterEvaluate {
    def checkFile = file(".gradle/loom-cache/SETUP")
    task lwjglJarDelayed {
        dependsOn lwjglJar
    }

    task setupGradle {
        dependsOn lwjglJarDelayed
        dependsOn genSourcesWithQuiltflower
        genSourcesWithQuiltflower.mustRunAfter lwjglJarDelayed
        doLast {
            checkFile.parentFile.mkdirs()
            checkFile.createNewFile()
        }
    }
    if (!checkFile.exists()) {
        logger.error("--------------")
        logger.error("PLEASE RUN THE `setupGradle` TASK, OR ELSE UNEXPECTED THING MAY HAPPEN!")
        logger.error("--------------")
    }
}